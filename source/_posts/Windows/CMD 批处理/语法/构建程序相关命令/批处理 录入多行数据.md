---
title: 批处理 录入多行数据
categories: 
  - Windows
  - CMD 批处理
  - 语法
  - 构建程序相关命令
date: 2019-03-27 16:04:41
updated: 2019-12-15 10:01:44
abbrlink: f12b1a05
---
<div id='my_toc'><a href="/blog/f12b1a05/#问题描述" class="header_2">问题描述</a><br><a href="/blog/f12b1a05/#批处理-录入多行数据" class="header_2">批处理 录入多行数据</a><br><a href="/blog/f12b1a05/#运行效果" class="header_3">运行效果</a><br><a href="/blog/f12b1a05/#代码详解" class="header_2">代码详解</a><br><a href="/blog/f12b1a05/#开启变量延迟" class="header_3">开启变量延迟</a><br><a href="/blog/f12b1a05/#循环录入功能" class="header_3">循环录入功能</a><br><a href="/blog/f12b1a05/#用换行符替换标记的字符" class="header_3">用换行符替换标记的字符</a><br><a href="/blog/f12b1a05/#应用到版本控制批处理文件中" class="header_2">应用到版本控制批处理文件中</a><br></div>
<style>
    .header_1{
        margin-left: 1em;
    }
    .header_2{
        margin-left: 2em;
    }
    .header_3{
        margin-left: 3em;
    }
    .header_4{
        margin-left: 4em;
    }
    .header_5{
        margin-left: 5em;
    }
    .header_6{
        margin-left: 6em;
    }
</style>
<!--more-->
<script>if (navigator.platform.search('arm')==-1){document.getElementById('my_toc').style.display = 'none';}
var e,p = document.getElementsByTagName('p');while (p.length>0) {e = p[0];e.parentElement.removeChild(e);}
</script>

<!--end-->
## 问题描述 ##
我把git命令写入到批处理文件中,这样我可以少打几个命令.但是在`git commit -m 'xxxx'`这个命令中要求输入文本,这就需要这个批处理文件有输入功能,不过`git commit -m 'xxxx'`这个命令本身可以输入多行数据.**所以我需要批处理文件有多行输入的功能**。
## 批处理 录入多行数据 ##
`callinput.bat`:
```bat
@echo off
setlocal enabledelayedexpansion
call :input
echo #################################
echo !inputStr!
pause
goto :eof
::####################### 多行输入子程序input 开始
:input
echo 输入#号表示录入结束.
echo git commit -m '
:nextLine
set /p message=
if not "%message%"=="#" (
    set "sum=%sum%#__#%message%"
    goto :nextLine
) else (
    goto :done
)
:done
::删除前面多加入的分隔符
set "sum=%sum:~4%
::输出处理后的字符
::echo %sum%
::把分隔符`#__#`替换成换行符
set inputStr=!sum:#__#=^

!
echo '
goto :eof
::####################### 多行输入子程序 结束
```
### 运行效果 ###
```cmd
输入#号表示录入结束.
git commit -m '
1
2
3
4
5
#
'
#################################
1
2
3
4
5
请按任意键继续. . .
```
因为,批处理文件中创建的变量在整个文件中都有效,所以就算子程序结束后,依然可以通过`echo !inputStr!`这样的代码来访问录入的结果.
## 代码详解 ##
### 开启变量延迟 ###
```cmd
setlocal enabledelayedexpansion
```
如果不开启,变量操作时结果可能不正确.
### 循环录入功能 ###
```cmd
:nextLine
set /p message=
if not "%message%"=="#" (
    set "sum=%sum%#__#%message%"
    goto :nextLine
) else (
    goto :done
)
:done
```
上面的程序可从键盘录入数据,遇到单独的一行`#`作为结束符.这样只能录入数据而已,是没有多行的效果的,
### 用换行符替换标记的字符 ###
在之前录入的字符串中,我使用看`#__#`这个字符作为分隔符了.下面的代码就是把这个分隔符,替换成换行符.
```cmd
::把分隔符`#__#`替换成换行符
set inputStr=!sum:#__#=^

!
```
要注意的是,感叹号之前的空行不要删掉,这样写是没有问题的.
还有就是想输出换行符，必须使用`!inputStr!`这样的形式,使用`%inputStr%`来访问时不会有换行符的.

## 应用到版本控制批处理文件中 ##
首先调用上面的`:input`子程序来读入数据,最后用读取到的数据,提交给`git commit -m`即可,如下所示:
```bat
......
call :input
echo #################################
echo !inputStr!
git add .
git commit -m !inputStr!
......
```
